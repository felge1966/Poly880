; POLY-COMPUTER 880
;
; 11. Prüfprogramme für den POLY-COMPUTER
; Bedienhandbuch, S. 67-79
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

;*************************************************************************
;
; 	TEST EINES ROMS IM POLY-COMPUTER
;
;*************************************************************************
;
; 	STRATEGIE: JEDER VOM VEB KOMBINAT POLYTECHNIK UND PRAEZISIONS-
; 		   GERAETE FUER DEN POLY-COMPUTER BEZOGENE ROM ENTHAELT
;		   AUF DEN ROMADRESSEN 3FCH, 3FDH UND 3FEH EINEN
;		   PRUEFKODE. DIESER STELLT DIE NEGATION DER SUMME ALLER
;		   BYTES DAR.
;		   DER TEST VERGLEICHT DIESE ABGESPEICHERTE SUMME
;		   MIT DER SELBST BERECHNETEN UND FUEHRT SCHLIESSLICH
;		   EINE ENTSPRECHENDE AUSGABE AUF DIE ANZEIGE AUS.
;		   DAS REGISTERPAAR HL MUSS BEIM AUFRUF DIE ANFANGS-
; 		   ADRESSE DES ROM IM SPEICHER ENTHALTEN.
;
;*************************************************************************
	ORG 	4000H
;	ld	hl,0
	LD 	BC,03FCH
	LD 	DE,0000H 	; ANFANGSWERT DER SUMMIERUNG
	LD 	A,00H 		; ANFANGSWERT DER SUMMIERUNG
	INC 	B
LSROM1 	ADD 	A,(HL)
	JR	NC,LSROM2
	INC 	DE
LSROM2 	INC 	HL 		; LESEZEIGER ERHOEHEN
	DEC 	C
	JR	NZ,LSROM1
	DEC	B
	JR	NZ,LSROM1
	CPL			; ES SIND DIE NEGIERTEN WERTE
	CP	(HL)		; IM ROM ABGESPEICHERT
	JR	NZ,LSROM3	; FEHLER
	LD	A,E
	CPL
	INC  	HL
	CP  	(HL)
  	JR	NZ,LSROM3
	LD  	A,D
	CPL
	INC  	HL
	CP  	(HL)
  	JR	NZ,LSROM3
	LD  	DE,7100H  	; FERTIG IN ANZEIGE
  	JR  	LSROM4
LSROM3  LD  	DE,7311H  	; ERROR IN ANZEIGE
LSROM4  CALL  	KOMDAR
LSROM5  CALL  	TASTU
	JR  	LSROM5
KOMDAR  EQU  	02ACH
TASTU	EQU  	014BH
	END
