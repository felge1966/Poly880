; POLY-COMPUTER 880
;
; 8. Die Nutzung des Fernschreiberanschlusses im POLY-COMPUTER 880
; Bedienhandbuch, S. 52-56
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

;*************************************************************************
;
;	UNTERPROGRAMME ZUR EIN- BZW. AUSGABE EINES 5-BIT ZEICHENS
;  	UEBER DEN FERNSCHREIBERANSCHLUSS (PIOD2 BIT 0)
;
;*************************************************************************
;	EMPFANG EINES ZEICHENS
;	ZEICHEN WIRD IN D4-D0 DES REGISTERS C UEBERGEBEN
;	ZERSTOERT: AF,BC,DE
;	STACKBEDARF: 4 (EINSCHLIESSLICH AUFRUF VON FREC)
;
	ORG 	4300H 		; ANFANGSADRESSE
PIOD2  	EQU 	082H 		; PIOADRESSE KANAL B DATEN
PIOC2  	EQU 	083H 		; PIOADRESSE KANAL B CONTROL
FREC  	IN 	A,PIOD2
	BIT 	0,A 		; TEST DER EMPFANGSLEITUNG
	JR	NZ,FREC		; AUF STARTBIT WARTEN
	LD 	DE,NTTY+NTTY/2 	; 1 1/2 BIT WARTEN
	CALL 	TIMEx
	BIT 	0,A 		; AUF STARTPOLARITAET PRUEFEN
	LD 	B,5 		; BITANZAHL
FREC1  	IN 	A,PIOD2		; LEITUNG IN BITMITTE ABTASTEN
	BIT 	0,A
	SCF
	JR	NZ, FREC2 	; 1 ERKANNT
	CCF 			; 0 ERKANNT
FREC2  	RR 	C 		; BIT IN REGISTER C SCHIEBEN 
	LD 	DE,NTTY 
	CALL 	TIMEx 
	DJNZ 	FREC1 		; 5 BIT LESEN 
	SRL 	C 
	SRL 	C 
	SRL 	C 		; ZEICHEN NORMALISIEREN 
	RET 
;*************************************************************************
;	AUSSENDEN EINES 5 BIT-ZEICHENS
;	ZEICHEN MUSS SICH IN D4-D0 VON REGISTER C BEFINDEN
;	ZERSTOERT AF,BC,DE
;	STACKBEDARF: 4 (EINSCHLIESSLICH DES AUFRUFS VON FSEND)
;
FSEND 	LD 	A,0CFH 		; MODE 3 DER PIO
	OUT 	PIOC2,A
	LD 	A,0BAH 		; FERNSCHREIBERANSCHLUSS AUF
	OUT 	PIOC2,A		; AUSGABE
	SLA 	C 		; STARTBIT
	SET 	6,C 		; 1. STOPBIT
	SET 	7,C 		; 2. STOPBIT
	LD 	B,8 		; BITANZAHL
FSEND1 	IN 	A,PIOD2
	RES 	0,A
	RRC 	C
	JR	NC,FSEND2 	; 0 SENDEN
	SET 	0,A 		; 1 SENDEN
FSEND2 	OUT 	PIOD2,A		; BIT SENDEN
	LD 	DE,NTTY
	CALL 	TIMEx 		; 1 BIT WARTEN
	DJNZ 	FSEND1		; 7 BIT SENDEN
	LD 	A,0CFH 		; MODE 3 DER PIO
	OUT 	PIOC2,A
	LD 	A, 0BBH 	; FERNSCHREIBERANSCHLUSS
	OUT 	PIOC2,A		; AUF EINGABE
	RET
NTTY 	EQU 	02C4H 		; ZAEHLWERT FUER EIN  BIT (20 MS)
;*************************************************************************
;
;	ZEITSCHLEIFE
;	VERZOEGERT 26*DE PROZESSORTAKTE
;	ZERSTOERT: AF,DE
;	STACKBEDARF: 2 BYTE
;
TIMEx 	DEC 	DE
	LD 	A, E
	OR 	D
	JR	NZ, TIMEx
	RET
	END


