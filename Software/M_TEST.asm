; POLY-COMPUTER 880
;
; 11. Prüfprogramme für den POLY-COMPUTER
; Bedienhandbuch, S. 67-79
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

;*************************************************************************
;
;	TESTPROGRAMM FUER DEN MAGNETBANDANSCHLUSS
;
;*************************************************************************
;
;	STRATEGIE: EIN- UND AUSGANG DES MAGNETBANDANSCHLUSSES SIND
;		   DURCH EINEN WIDERSTAND VON 10K MITEINANDER
;		   ZU VERBINDEN. DAS TESTPROGRAMM SENDET EIN SIGNAL
;		   MIT EINER FREQUENZ VON ETWA 100 HZ UND BEWERTET
;		   DIE OBERE UND DIE UNTERE GRENZFREQUENZ. FEHLER
;		   WERDEN AUSGEGEBEN. DURCH DEN WIDERSTAND KANN DIE
;		   EMPFINDLICHKEIT DES VERFAHRENS BEEINFLUSST WERDEN.
;		   (KEINE FEHLER BEI R=10K BEDEUTET EINE EMPFINDLICHKEIT
;		   VON ETWA 50 MILLIVOLT.)
;
;*************************************************************************
	ORG 	4000H
	LD 	DE,1000H 	; ANZAHL DER TESTBITS
LSMB1  	IN 	A,PIOD2
 	XOR 	04H 		; AUSGABE UMSCHALTEN
 	LD 	L,A
 	OUT 	PIOD2,A
 	LD 	B,04H
 	DJNZ 	$		; 60 MIKROSEKUNDEN WARTEN
 	IN 	A,PIOD2		; BIT ABTASTEN
	RLCA
	XOR 	L
	BIT  	2,A
	JR	Z,LSMB5		; FEHLER DER OBEREN GRENZFREQUENZ
	LD  	BC,0200H
LSMB2  	DEC  	BC
	LD  	A,C
	OR  	B
	JR	NZ,LSMB2
	IN  	A,PIOD2
	RLCA
	XOR  	L
	BIT  	2,A
	JR	Z,LSMB6  	; FEHLER DER UNTEREN GRENZFREQUENZ
	DEC  	DE
	LD 	A,E
	OR  	D
	JR	NZ,LSMB1
	LD  	DE,7100H  	; FERTIG IN  ANZEIGE
LSMB3  	CALL  	KOMDAR
LSMB4  	CALL  	TASTU
	JR  	LSMB4
LSMB5  	LD  	DE,73E7H 	; FEHLER OBERE GRENZFREQUENZ
	JR  	LSMB3
LSMB6  	LD  	DE,73C7H  	; FEHLER UNTERE GRENZFREQUENZ
	JR  	LSMB3
KOMDAR  EQU  	02ACH
TASTU  	EQU  	014BH
PIOD2   EQU  	82H
PIOC2  	EQU  	83H
	END
