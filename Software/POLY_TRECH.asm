; POLY-COMPUTER	880
;
; 10.3.1. Der POLY-COMPUTER als einfacher Taschenrechner
; Arbeitshandbuch, S. 208-222
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

;
; PROGRAMMIERBEISPIEL "TASCHENRECHNER" 
;
; MODUL "ZAHL EINLESEN" 
;	UEBERGABE REGISTER X - HL 
;		(REGISTER Y - DE: NICHT AENDERN) 
;	AUFGABE: ANZEIGE DES X-REGISTERS 
;		EINLESEN EINES NEUEN X-INHALTS 
;		BIS OPERATIONSTASTE ERKANNT 
; 	
 	ORG  	4000H 
ZAHL 	PUSH  	DE 
 	PUSH  	HL 		;X,Y IN STACK RETTEN 
 	CALL  	LOANZ 		;ANZEIGE LOESCHEN 
 	LD  	A,5 		;5 STELLEN ANZEIGEN 
ZAHL1 	LD  	BC,10 		;FAKTOR 10 DEZIMAL 
 	LD  	DE,0FFFFH 	;QUOTIENT:=-1 
ZAHL2 	INC  	DE 
 	AND  	A 		;CY:=0 
 	SBC  	HL,BC 
 	JR	NC,ZAHL2 
 	ADD  	HL,BC 		;IN HL DIV.-REST 
 	PUSH  	HL 		;ABSPEICHERN 
 	EX  	DE,HL 
 	DEC  	A 
 	JR	NZ,ZAHL1 
;KONVERTIERTE ZAHL ANZEIGEN 
 	LD  	C,5 		;5 STELLEN 
ZAHL3 	POP  	DE 		;ZIFFER IN E 
 	LD  	A,E 
 	LD  	B,7 
 	LD  	HL,ANZ 
 	CALL  	ZIFANZ 		;ZIFFER IN ANZEIGE 
 	DEC  	C 
 	JR	NZ,ZAHL3 
;TASTATURABFRAGE, NEUES X ODER OPERATION 
ZAHL4 	LD  	DE,ANZ 
 	CALL  	KONSOL 
 	JR	Z,ZAHL4 	;WARTEN BIS TASTE BETAET. 
 	LD  	A,C 		;HEX. TASTENKODE 
 	CP  	0AH 
 	JR	NC,ZAHL6 	;SPRUNG. WENN TASTE A..F 
 	CALL  	LOANZ 
 	LD  	HL,0 		;X - ANFANGSWERT 
ZAHL5 	PUSH  	HL 
 	PUSH  	AF 
 	LD  	B,7 
 	LD  	HL,ANZ 
 	CALL  	ZIFANZ 		;EINGEG. ZIFFER ANZEIGEN 
 	POP  	AF 
 	POP  	HL 
 	LD  	E,A 
 	LD  	D,0 		;NEUE ZIFFER NACH DE 
 	LD  	B,10 		;FAKTOR 10 
 	EX  	DE,HL 
ZAHL7 	ADD  	HL,DE 		;X:=(10*X)+ZIFFER 
 	JR	C,ZAHLF		;ZAHL ZU GROSS 
	;RUECKKEHR MIT FEHLERMARKIERUNG CY=1 
	DJNZ 	ZAHL7 
	PUSH 	HL 
;TASTATURABFRRGE MACH WEITEREN ZIFFERN 
ZAHL8  	LD 	DE,ANZ 
	CALL 	KONSOL 
	JR	Z,ZAHL8 
	POP 	HL 
	LD 	A,C 
	CP 	0AH 
	JR	C,ZAHL5 	;SPRUNG.WENN WEITERE ZIFFER
	EX 	(SP),HL 	;NEUES X ERSETZT ALTES 
ZAHL6  	AND 	A 		;CY:=0,KEIN FEHLER 
ZAHLF  	POP 	HL 
	POP 	DE 		;X,Y IN HL, DE 
	RET 
	ORG 	$+6 

;HILFSPROZEDUR "LOESCHEN DER ANZEIGE" 
LOANZ  	PUSH 	HL 		;HL RETTEN 
	LD 	B,8 		;8 ANZEIGESTELLEN 
	LD 	HL,ANZ 
LO1  	LD 	(HL),0 
	INC 	HL 
	DJNZ 	LO1 
	POP 	HL 
	RET 
	ORG 	$+6 
;
;ANZEIGEBEREICH 
ANZ 	DS 	8 		;8 BYTE ANZEIGEBEREICH 
;
;STARTADRESSEN DER MONITOR-UNTERPROGRAMME 
;(SIEHE BEDIENBESCHREIBUNG) 
KONSOL  EQU 	14EH 
ZIFANZ  EQU 	300H 



; MODUL "OPERATION AUSWERTEN" 
;	UEBERGABE: A - FUNKTIONSKODIERUNG 0A-0F
;	RUECKGABE: IX - STARTADRESSE DES FUNKTIONS-UP 
;		DE, HL DUERFEN NICHT VERAENDERT WERDEN 
;	ALGORITHMUS: AUSLESEN EINER LISTE DER START- 
;		ADRESSEN MIT EINEM ZEIGER :=
;		(KODIERUNG-0AH)*2 + LISTENANFANGSADRESSE 

OPAUSW  PUSH 	HL 
	LD 	HL,LISTE 
	SUB 	0AH 		;A:=KODIERUNG-0AH
	RLCA 	 		;A:=2*A
	LD 	C,A 
	LD 	B,0 		;ERG. NACH BC 
	ADD 	HL,BC 
	LD 	C,(HL) 		;NIEDERWERT.TEIL 
	INC 	HL 
	LD 	B,(HL) 		;HOEHERWERT. TEIL 
	PUSH 	BC 
	POP 	IY 		;STARTADRESSE IN IY
	POP 	HL 
	RET 
	ORG 	$+6 
;
;LISTE DER STRRTAORESSEN DER FUNKTIONEN 
; IN DER REIHENFOLGE DER TASTENKODIERUNGEN 
;
LISTE  	DW 	ADDW		;TASTE "A"
	DW 	SUBW 		;TASTE "B" 
	DW 	MULW 		;TASTE "C" 
	DW 	DIVW 		;TASTE "D" 
	DW 	EINGAB 		;TASTE "E" 
	DW 	FREI 		;TASTE "F"
;
;HAUPTRROGRAMM TASCHENRECHNER 
;
TARECH  CALL 	ZAHL 
	JR	C,FEHLER 
	CALL 	OPAUSW 
	LD 	BC,TARE1  	;RUECKKEHRADRESSE DER UP 
	PUSH 	BC 		;AUF STACK ABSPEICHERN 
	JP 	(IY)	 	;UP ANSPRINGEN
TARE1  	JR	NC,TARECH 	;WEITERRECHNEN, WENN 
				;KEIN FEHLER 
;ANZEIGE EINES FEHLERS 
FEHLER  PUSH 	HL 
	PUSH 	DE 
	LD 	HL,ANZ 
	LD 	DE,07173H 	;K0DE FUER ANZEIGE "FE" 
	CALL 	FUNKAN 		;ANZEIGEN 
FE1  	LD 	DE,ANZ 
 	CALL 	KONSOL 		;WARTEN AUF TASTATURBETAETIGUNG
	JR	Z,FE1 		;ZUR  FEHLEROUITTIERUNG 
	POP 	DE 
	POP 	HL 
	JR 	TARECH 
;UNTERPROGRANN FUNKAN DES MONITORS 
FUNKAN  EQU 	2AFH 
	ORG 	$+6




;MODUL "ARITHMETIK" 
; ALLGEMEINE MERKMALE: 
;	1.OPERAND - DE 
;	2.OPERRND - HL
;	ERGEBNIS - HL 
;	BEI UNZULAESSIGER OPERATION: CY:=1 
;
;16-BIT-ADDITION 
ADDW 	ADD 	HL,DE
	RET 
 	ORG 	$+6 
;
;16-BIT-SUBTRAKTION 
SUBW 	EX 	DE,HL 
	AND 	A 		;CY:=0 
	SBC 	HL,DE 
	RET 
 	ORG 	$+6 
;
;16-BIT-MULTIPLIKATION 
MULW 	LD 	B,H 
	LD 	C,L 		;FAKTOR NACH BC 
	LD 	HL,0 		;ANF.WERT PRODUKT 
MULW1 	LD 	A,B
	OR 	C 		;A=0,WENN BC=0 
	RET	Z 
	DEC 	BC 		;FAKTOR HERUNTERZAEHLEN 
	ADD 	HL,DE 
	RET	C		;UEBERLAUF 
	JR 	MULW1 
 	ORG 	$+6 
;
;16-BIT-DIVISION 
DIVW 	EX 	DE,HL 
	LD 	A,D 
	OR 	E 		;A=0, WENN DIV. DURCH 0 
	JR	Z,DIVW0
	LD 	BC,0FFFFH 	;ANF.WERT QUOTIENT 
DIVW1 	INC 	BC 
	AND 	A 		;CY:=0 
	SBC 	HL,DE 
	JR	NC,DIVW1 
	LD 	H,B
	LD 	L,C 		;QUOTIENT NACH HL 
	AND 	A 		;CY:=0
	RET 
DIVW0 	SCF 			;CY:=1,FEHLER 
	RET 
 	ORG 	$+6 
;
;EINGABE, REGISTERTAUSCH 
EINGAB 	EX 	DE,HL 
	AND 	A 		;CY:=0 
	RET 
 	ORG 	$+6 

;RESERVEFUNKTION 
FREI 	AND 	A 		;CY:=0 
	RET 

	END	TARECH
	