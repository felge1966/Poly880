; POLY-COMPUTER	880
;
; 8.8.2. Programbeispiel-Unterbrechungsgesteuerte Digitaluhr 
; Arbeitshandbuch, S. 169-176
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

; PROGRAMM ZUR REALISIERUNG EINER
; DIGITALUHR. MIT CTC-BENUTZUNG
; STELLMOEGLICHKEIT MIT	MONITOR
;
;UNTERBRECHUNGSBEHANDLUNGSPROGRAMM
	ORG	4000H
UBR	PUSH	AF
	PUSH	HL		;BENUTZTE REG. RETTEN
	LD	HL,HSEK
	DEC	(HL)		;1/100S	BINAER ZAEHLEN
	JR	NZ,UBR1
	LD	(HL),100	;UNTERSETZ.-FRKTOR
	INC	HL		;HL ZEIGT AUF SEK.
	LD	A,(HL)
	ADD	A,1		;SEKUNDEN WEITERSCHALTEN
	DAA
	LD	(HL),A
	SUB	60H		;60S ERREICHT ?
	JR	NZ,UBR1
	LD	(HL),A
	INC	HL
	LD	A,(HL)
	ADD	A,1		;MINUTEN WEITERSCHALTEN
	DAA
	LD	(HL),A
	SUB	60H		;60MIN ERREICHT	?
	JR	NZ,UBR1
	LD	(HL),A
	INC	HL
	LD	A,(HL)
	ADD	A,1		;STUNDEN WLITERSCHALTEN
	DAA
	LD	(HL),A
	SUB	24H		;24 STUNDEN ERREICHT ?
	JR	NZ,UBR1
	LD	(HL),A
UBR1	POP	HL
	POP	AF		;REG. WIEDERHERSTELLEN
	EI
	RETI

 ;HINTERGRUNDPROGPAMM
 ;CTC-PROGRAMMIERUNG
 ;ANZEIGEKONVEPTIERUNG
	ORG	4040H
UHRPRO	LD	A,41H
	LD	I,A		;I-REG.	LADEN
	IM	2
	LD	A,0		;UBR.-VEKTOR FUER CTC
	OUT	(38H),A		;IN KANAL 0 LADEN
	LD	A,0A7H		;KANALSTEUERWORT
	OUT	(89H),A
	LD	A,24H		;ZEITKONST. 36
	OUT	(89H),A
	EI
;ANZEIGESCHLEIFE
;STAENDIGE AFTUALiSIERUNU DER ANZEIGE
ANZE	LD	HL,STUN
	XOR	A
	LD	BC,ANZBER
	LD	(BC),A
	INC	BC
	LD	(BC),A		;ERSTE 2 ANZEIGEN LOESCHEN
	INC	BC
	LD	A,(HL)		;STUNDEN
	CALL	ANZKON
	DEC	HL
	LD	A,(HL)		;MINUTEN
	CALL	ANZKON
	DEC	HL
	LD	A,(HL)		;SEKUNDEN
	CALL	ANZKON
	LD	DE,ANZBER
	CALL	KONSOL		;ANZEIGE ANSTEUERN
	JR	ANZE
;
; UNTERPROGRAMM	ZUM KONVERTIEREN
; EINES	BYTES IN DIE 7-SEG.-ANZEIGE
;  EINGABE : ANZUZEIGENDES BYTE	IN A
;  AUSGABE : ANZEIGESTELLEN (BC),(BC+1)
;		       BC := BC	+ 2
; VERAENDERTE REGISTER:	AF,BC
	ORG	4080H
ANZKON	PUSH	HL
	PUSH	DE
	LD	DE,ANZDEC	;7-SEG.-TABELLE
	PUSH	AF
	RRCA
	RRCA
	RRCA
	RRCA			;HOEHERWERT. HALBBYTE RECHTSBUENDIG
	AND	0FH
	LD	H,0
	LD	L,A		;HL -VERSATZ FUER TABELLE
	ADD	HL,DE
	LD	A,(HL)		;7-SEG.-KODE
	AND	0F7H		;PUNKT LOESCHEN
	LD	(BC),A		;IN  ANZEIGEBEREICH
	INC	BC
	POP	AF
	AND	0FH		;NIEDERWERT. HALBBYTE
	LD	H,0
	LD	L,A
	ADD	HL,DE
	LD	A,(HL)
	LD	(BC),A
	INC	BC
	POP	DE
	POP	HL
	RET

;SPEICHERSTELLEN
	ORG	40C0H
HSEK	DB	0
SEK	DB	0
MIN	DB	30
STUN	DB	14
;
;TABELLE DER UNTERBRECHUNGSPROGRAMME
	ORG	4100H
UBRTAB	DW	0
	DW	4000H		;BENUTZTER KANAL 1
	DW	0
	DW	0
;
;BEREICH FUER ANZEIGEBELEGUNG
ANZBER	EQU	4120H		;ANZEIGEBEREICH	IM RAM
;
;BENUTZTE PROGRAMME UND	BEREICHE
;DES MONITORPROGRAMMS
KONSOL	EQU	14EH		;ANZEIGEANSTEUERPROG.
ANZDEC	EQU	310H		;7-SEG.-KODETABELLE
	END	UHRPRO
