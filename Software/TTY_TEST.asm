; POLY-COMPUTER 880
;
; 11. Prüfprogramme für den POLY-COMPUTER
; Bedienhandbuch, S. 67-79
;
; assemblieren mit Arnold-Assembler www.alfsembler.de
; vp120210
;
	cpu	z80

;*************************************************************************
;
; 	TESTPROGRAMM FUER FERNSCHREIBERANSCHLUSS
;
;*************************************************************************
;
;	STRATEGIE: DAS TESTPROGRAMM ERZEUGT STATISCH DIE DREI MOEGLICHEN
;		   ZUSTAENDE AM FERNSCHREIBERANSCHLUSS, DER UEBERGANG
;		   ZUM NAECHSTEN ZUSTAND ERFOLGT DURCH BETAETIGUNG DER
;		   TASTE "EXEC". IN DER ANZEIGEEINHEIT WIRD DER BESTEHENDE
;		   ZUSTAND DARGESTELLT: O1 = AUSGABE HOCH
;					O0 = AUSGABE TIEF
;					IX = EINGABE HOCH/TIEF
;
;*************************************************************************
	ORG 	4000H
LSFS1 	LD 	A,0CFH		; PIO AUF AUSGABE
 	OUT 	PIOC2,A
 	IN 	A,PIOD2
 	SET 	0,A
 	OUT 	PIOD2,A		; AUSGABE HOCH
 	LD 	A,0BAH
 	OUT 	PIOC2,A
 	LD 	DE,0E784H 	; IN ANZEIGE: O1 = AUSGABE HOCH
 	CALL 	KOMDAR
LSFS2 	CALL 	TASTU
 	CP 	41H 		; KODE "EXEC"
 	JR	NZ,LSFS2
 	IN  	A,PIOD2
 	RES 	0,A
	OUT  	PIOD2,A  	; AUSGABE TIEF
	LD  	DE,0E7E7H  	; IN ANZEIGE: O0 = AUSGABE TIEF
	CALL  	KOMDAR
LSFS3  	CALL  	TASTU
	CP  	41H
 	JR	NZ,LSFS3  	; AUF "EXEC" WARTEN
 	LD  	A,0CFH
 	OUT  	PIOC2,A
 	LD  	A,0BBH  	; PIO AUF EINGABE
 	OUT  	PIOC2,A
LSFS4  	IN  	A,PIOD2
 	BIT  	0,A
 	LD  	DE,41E7H  	; I 0 IN ANZEIGE (EINGABE TIEF)
 	JR	Z,LSFS5
 	LD  	E,84H  		; I 1 IN ANZEIGE (EINGABE HOCH)
LSFS5  	CALL  	KOMDAR
 	CALL  	TASTU
 	CP  	41H
 	JR	NZ,LSFS4  	; AUF "EXEC" WARTEN UND EINGABE DARSTELLEN
 	JP  	LSFS1
KOMDAR  EQU  	02ACH
TASTU  	EQU  	014BH
PIOD2  	EQU  	82H
PIOC2  	EQU  	83H
 	END
